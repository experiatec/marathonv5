/*
Copyright 2016 Jalian Systems Pvt. Ltd.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

repositories {
  mavenCentral()
}

apply plugin: 'distribution'

dependencies {
  compile project(":marathon-core")
  runtime project(":marathon")
  runtime project(":marathon-java")
  runtime project(":marathon-web")
  runtime project(":marathon-web:marathon-web-runtime")
  runtime project(":marathon-java:marathon-java-driver")
  runtime project(":marathon-java:marathon-java-agent")
  runtime project(":marathon-java:marathon-java-recorder")
  runtime project(":marathon-java:marathon-stb-inst")
  runtime project(":marathon-java:marathon-java-runtime")
  runtime project(":marathon-javafx")
  runtime project(":marathon-javafx:marathon-javafx-agent")
  runtime project(":marathon-javafx:marathon-javafx-recorder")
  runtime project(":marathon-javafx:marathon-javafx-runtime")
  runtime project(":marathon-core:ruby-selenium-webdriver")
}

jar.doFirst{
  manifest {
    def classpath = configurations.runtime.filter{ it.name.startsWith 'marathon-' }.collect { it.name } +
              configurations.runtime.filter{ !it.name.startsWith('marathon-') }.collect { 'support/' + it.name }
    Properties props = new Properties()
    props.load(new FileInputStream(file('build.properties')))
    props.put("marathonversion", "${version}")
    attributes("Manifest-Version" : "1.0",
              "Created-By" : props.get('blurbcompany'),
              "Specification-Title" : props.get('blurbtitle'),
              "Specification-Version" : props.get('marathonversion'),
              "Specification-Vendor" : props.get('blurbcompany'),
              "Implementation-Title" : props.get('marathonproduct'),
              "Implementation-Version" : props.get('marathonversion'),
              "Implementation-Vendor" : props.get('blurbcompany'),
              "Main-Class" : "net.sourceforge.marathon.Main",
              "Class-Path" : classpath.join(' ')
    )
  }
}

task startScripts(type: Copy) {
    destinationDir file("$buildDir/scripts")
    from('script-templates/marathon') {
      expand( [ 'marathonJar' : jar.archiveName, ])
      fileMode(0755)
    }
    from('script-templates/marathon.bat') {
      expand( [ 'marathonJar' : jar.archiveName, ])
    }
    from('script-templates/simple-web-server') {
      expand( [ 'marathonJar' : jar.archiveName, ])
      fileMode(0755)
    }
    from('script-templates/simple-web-server.bat') {
      expand( [ 'marathonJar' : jar.archiveName, ])
    }
    from('script-templates/mruby') {
      expand( [ 'rubyJar' : "jruby-complete-${jruby_complete_version}.jar", "version" : "${version}" ])
      fileMode(0755)
    }
    from('script-templates/mruby.bat') {
      expand( [ 'rubyJar' : "jruby-complete-${jruby_complete_version}.jar", "version" : "${version}" ])
    }
    from('script-templates/mirb') {
      expand( [ 'rubyJar' : "jruby-complete-${jruby_complete_version}.jar", "version" : "${version}" ])
      fileMode(0755)
    }
    from('script-templates/mirb.bat') {
      expand( [ 'rubyJar' : "jruby-complete-${jruby_complete_version}.jar", "version" : "${version}" ])
    }
    from('script-templates/mgem') {
      expand( [ 'rubyJar' : "jruby-complete-${jruby_complete_version}.jar", "version" : "${version}" ])
      fileMode(0755)
    }
    from('script-templates/mgem.bat') {
      expand( [ 'rubyJar' : "jruby-complete-${jruby_complete_version}.jar", "version" : "${version}" ])
    }
}

distributions {
  main {
    contents {
      duplicatesStrategy = "exclude"
      from(configurations.runtime) {
        exclude('**/marathon*jar')
        into('support')
      }
      from(configurations.runtime) {
        include('**/marathon*jar')
      }
      from(project(':marathon-jenkins').file('build/libs'))
      from(project(':marathon-core').file('src/main/dist'))
			if(project.properties['jxbrowser_install_path']) {
      	from(project(':marathonext-jxbrowser').jar)
      	from(project(':marathonext-jxbrowser').file('src/main/dist'))
			}
      from(project(':marathon-java:marathon-java-runtime').file('src/main/dist'))
      from(project(':marathon-javafx:marathon-javafx-runtime').file('src/main/dist'))
      from(startScripts) {
        fileMode = 0755
      }
      from(project(':marathon-web').file('src/main/dist'))
      from(project(':marathon-web').file('chrome-extension/marathon-chrome-extension.crx'))
      from(project(':marathon-java:marathon-kbmap-tool').configurations.runtime) {
        exclude('**/marathon*jar')
        into('support')
      }
      from(project(':marathon-java:marathon-kbmap-tool').configurations.runtime) {
        include('**/marathon*jar')
      }
      from(project(':marathon-java:marathon-kbmap-tool').jar)
      from(project(':marathon-java:marathon-kbmap-tool').files('build/scripts'))
      from(project(':marathon-core').file('ChangeLog'))
    }
  }
}

task updateVersions(type: Copy) {
    inputs.file file("build.properties")
    inputs.file file("../build.gradle")
    Properties props = new Properties()
    props.load(new FileInputStream(file('build.properties')))
    props.put("buildtime", new Date().format("dd MMM yyyy hh:mm aa"))
    props.put("marathonversion", "${version}")
    from project(':marathon-core').file('Version.tmpl')
    into file('src/main/java').path
    expand(props)
    rename('Version.tmpl', 'Version.java')
}

compileJava.dependsOn(updateVersions)
assembleDist.dependsOn(startScripts)
distZip.dependsOn(project(':marathon-java:marathon-kbmap-tool').tasks.build)
tasks.eclipse.dependsOn(updateVersions)
distZip.dependsOn(project(':marathon-jenkins').tasks.build)
